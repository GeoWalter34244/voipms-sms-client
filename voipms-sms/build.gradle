plugins {
    id 'com.google.devtools.ksp'
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "net.kourlas.oss-licenses-plugin"

android {
    compileSdk 35
    defaultConfig {
        applicationId "net.kourlas.voipms_sms"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 149
        versionName "0.6.28"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
            arg("room.incremental", "true")
        }
    }
    flavorDimensions = ["version", "demo"]
    productFlavors {
        // fdroid-remove-start
        primary {
            dimension "version"
            buildConfigField "boolean", "IS_FDROID", "false"
        }
        // fdroid-remove-end
        fdroid {
            dimension "version"
            versionNameSuffix "-fdroid"
            buildConfigField "boolean", "IS_FDROID", "true"
        }
        full {
            dimension "demo"
            buildConfigField "boolean", "IS_DEMO", "false"
            buildConfigField "boolean", "IS_DEMO_SENDING", "false"
        }
        demoNotSending {
            dimension "demo"
            applicationId "net.kourlas.voipms_sms.demo"
            buildConfigField "boolean", "IS_DEMO", "true"
            buildConfigField "boolean", "IS_DEMO_SENDING", "false"
            versionNameSuffix "-demo"
        }
        demoSending {
            dimension "demo"
            applicationId "net.kourlas.voipms_sms.demo"
            buildConfigField "boolean", "IS_DEMO", "true"
            buildConfigField "boolean", "IS_DEMO_SENDING", "true"
            versionNameSuffix "-demo"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        def isDemo = (names.contains("demoSending")
                || names.contains("demoNotSending"))
        def isRelease = variant.buildType.name == "release"
        def isPrimary = names.contains("primary")
        if (isDemo && (isPrimary || isRelease)) {
            variant.setIgnore(true)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.4"
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        // fdroid-remove-start
        primary.java.srcDirs += "src/primary/kotlin"
        // fdroid-remove-end
        fdroid.java.srcDirs += "src/fdroid/kotlin"
    }
    lint {
        abortOnError false
    }
    namespace 'net.kourlas.voipms_sms'
}

dependencies {
    def room_version = "2.6.1"
    def moshi_version = "1.15.2"

    // Kotlin libraries
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1"

    // Android support libraries
    implementation "androidx.annotation:annotation:1.9.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.compose.ui:ui:1.7.6"
    implementation "androidx.core:core-ktx:1.15.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    implementation "androidx.sharetarget:sharetarget:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.work:work-runtime-ktx:2.10.0"
    implementation "com.google.android.material:material:1.12.0"
    ksp "androidx.room:room-compiler:$room_version"

    // fdroid-remove-start

    // Firebase libraries
    primaryImplementation platform("com.google.firebase:firebase-bom:33.8.0")
    primaryImplementation "com.google.firebase:firebase-analytics-ktx"
    primaryImplementation "com.google.firebase:firebase-crashlytics-ktx"
    primaryImplementation "com.google.firebase:firebase-messaging-ktx"

    // fdroid-remove-end

    // Other third-party libraries
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.github.mukeshsolanki:MarkdownView-Android:2.0.0"
    implementation "com.github.xabaras:RecyclerViewSwipeDecorator:1.3"
    implementation "me.saket:better-link-movement-method:2.2.0"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
}

tasks.register('cleanAssets', Delete) {
    delete 'src/main/assets'
}

tasks.register('copyToAssets', Copy) {
    from '../PRIVACY.md'
    from '../CHANGES.md'
    from '../NOTICE'
    from '../LICENSE.md'
    from '../HELP.md'
    into 'src/main/assets'
}

preBuild.dependsOn copyToAssets
copyToAssets.dependsOn cleanAssets

android.applicationVariants.configureEach { variant ->
    def generatePackageLicenses = tasks.register("generatePackageLicenses${variant.name.capitalize()}", Exec) {
        commandLine 'python', '../licenses/packageLicenseParser.py', "${variant.name}"
    }.get()

    tasks.matching { it.name == "${variant.name}OssLicensesTask" }.configureEach { task ->
        generatePackageLicenses.dependsOn task
    }

    tasks.matching { it.name == "generate${variant.name.capitalize()}Assets" }.configureEach { task ->
        task.dependsOn generatePackageLicenses
    }

    generatePackageLicenses.dependsOn cleanAssets
}

// fdroid-remove-start
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
// fdroid-remove-end